*milfeulle.txt*		カーソル位置を保存しておき移動する為のプラグインです


==============================================================================
概要						*milfeulle-introduction*

*milfeulle.vim* はカーソル位置の履歴を保存しておき、そのカーソル位置へと移動す
る為のプラグインです。
同等の機能として |jumplist| がありますが |jumplist| とは違い、

- 現在のウィンドウでバッファを開く

のではなくて、

- 保存を行ったウィンドウの位置へカーソル位置が移動

を行うことができます。
また、この挙動は |milfeulle-jumper| や |milfeulle-kind| で拡張・変更を行うこと
が可能です。


==============================================================================
使い方						*milfeulle-usage*

Example: >
	nmap <C-o> <Plug>(milfeulle-prev)
	nmap <C-i> <Plug>(milfeulle-next)
<

デフォルトでは以下のタイミングでカーソル位置が保存されます。

- |CursorHold|
- |InsertLeave|

また、内部ではタブ番号、ウィンドウ番号を保持している為、その番号が変更された場
合は正しくカーソルの移動が行われません。


==============================================================================
移動ロジック					*milfeulle-jumping*

	カーソル位置の情報は現在の位置の前に追加されていきます。
>
	  0 tabpage: 2  winnr: 1  line:  10 bufnr: 6  name:main.vim
	  1 tabpage: 3  winnr: 1  line:  27 bufnr: 8  name:milfeulle.vim
	  2 tabpage: 5  winnr: 1  line: 193 bufnr: 4  name:milfeulle.vim
	> 3 
<
	"> " は現在の位置を指します。
	この時に |:MileullePrev| を行うと 2番目の位置へと移動し、
>
	  0 tabpage: 2  winnr: 1  line:  10 bufnr: 6  name:main.vim
	  1 tabpage: 3  winnr: 1  line:  27 bufnr: 8  name:milfeulle.vim
	> 2 tabpage: 5  winnr: 1  line: 193 bufnr: 4  name:milfeulle.vim
	  3 
<
	のような状態になります。
	この時に新しいカーソル位置の情報が追加された場合、

 >
 	  0 tabpage: 2  winnr: 1  line:  10 bufnr: 6  name:main.vim
 	  1 tabpage: 3  winnr: 1  line:  27 bufnr: 8  name:milfeulle.vim
 	  2 tabpage: 2  winnr: 1  line:  14 bufnr: 6  name:main.vim
	> 3 tabpage: 5  winnr: 1  line: 193 bufnr: 4  name:milfeulle.vim
	  4 
<
	のように 2番目の位置に追加されます。
	

==============================================================================
インターフェース				*milfeulle-interface*

----------------------------------------------------------------------------
コマンド					*milfeulle-commands*

MilfeulleDisp					*:MilfeulleDisp*
	ジャンプリストを表示します。
Example:>
	   0 tabpage: 2  winnr: 1  line:  10 bufnr: 6  name:main.vim
	   1 tabpage: 2  winnr: 1  line:  14 bufnr: 6  name:main.vim
	>  2 tabpage: 3  winnr: 1  line:  27 bufnr: 8  name:milfeulle.vim
	   3 tabpage: 5  winnr: 1  line: 193 bufnr: 4  name:milfeulle.vim
	   4 
<
	左側の "> " は現在の位置になります。
	上記の状態で |:MilfeullePrev| を実行すると 1番目へ
	|:MilfeulleNext| を実行すると 3番目へ移動します。

MilfeulleOverlay [{jumpername}]			*:MilfeulleOverlay*
	カーソル位置を記憶します。
	2回連続して同じ位置は記憶しません。
	{jumpername} には |milfeulle-jumper-list| を渡すことができます。
	{jumpername} がない場合は、|g:milfeulle_default_jumper_name| に設定さ
	れている値が使用されます。

MilfeulleClear					*:MilfeulleClear*
	履歴を削除します。

MilfeullePrev					*:MilfeullePrev*
	前のカーソル位置へ移動します。

MilfeulleNext					*:MilfeulleNext*
	次のカーソル位置へ移動します。

MilfeulleRefresh				*:MilfeulleRefresh*
	無効な移動先を削除します。


------------------------------------------------------------------------------
関数						*milfeulle-functions*

milfeulle#jump([{kind}, {index}])		*milfeulle#jump()*
	{kind} の {index} 位置へ移動します。
	{index} は |:MilFeulleDisp| で左側に表示されている番号です。
	{kind} がない場合は |g:milfeulle_default_kind| が使用されます。
	{index} がない場合は現在選択されている位置
	（|:MilFeulleDisp| で > を指している位置）に移動します。
	移動に失敗した場合は、-1 が返ってきます。

milfeulle#overlay({kind}, {jumper}])			*milfeulle#overlay()*
	{jumper} を {kind} に追加します。
	{kind} がない場合は |g:milfeulle_default_kind| が使用されます。
	{jumper} がない場合は |g:milfeulle_default_jumper_name| に設定されてい
	る名前の |milfeulle-jumper| が使用されます。

milfeulle#jumper_list()				*milfeulle#jumper_list()*
	|milfeulle-jumper-list| の名前のリストを返します。

------------------------------------------------------------------------------
キーマッピング					*milfeulle-key-mappings*

<Plug>(milfeulle-prev)				*<Plug>(milfeulle-prev)*
	前のカーソル位置へ移動します。

<Plug>(milfeulle-next)				*<Plug>(milfeulle-next)*
	次のカーソル位置へ移動します。

<Plug>(milfeulle-overlay)			*<Plug>(milfeulle-overlay)*
	カーソル位置を記憶します。
	2回連続して同じ位置は記憶しません。

<Plug>(milfeulle-clear)				*<Plug>(milfeulle-clear)*
	履歴を削除します。

<Plug>(milfeulle-refresh)			*<Plug>(milfeulle-refresh)*
	無効な移動先を削除します。


==============================================================================
設定						*milfeulle-setting*

------------------------------------------------------------------------------
変数						*milfeulle-variables*

g:milfeulle_history_size			*g:milfeulle_history_size*
	履歴のサイズです。
Default: >
	let g:milfeulle_history_size = 50
<

g:milfeulle_enable_CursorHold			*g:milfeulle_enable_CursorHold*
	1 が設定されていれば |CursorHold| でカーソル位置を保存します。
Default: >
	let g:milfeulle_enable_CursorHold = 1
<

g:milfeulle_enable_InsertLeave			*g:milfeulle_enable_InsertLeave*
	1 が設定されていれば |InsertLeave| でカーソル位置を保存します。
Default: >
	let g:milfeulle_enable_InsertLeave = 1
<

g:milfeulle_default_jumper_name			*milfeulle_default_jumper_name*
	デフォルトで使用される |milfeulle-jumper-list| の名前です。
Default: >
	let g:milfeulle_default_jumper_name = "win_tab_bufnr_pos"
<

g:milfeulle_default_kind			*milfeulle_default_kind*
	デフォルトで使用される |milfeulle-kind| の名前です。
Default: >
	let g:milfeulle_default_kind = "global"
<


==============================================================================
移動方法					*milfeulle-jumper*

|milfeulle#overlay()| には移動方法を記述した辞書を渡すことで、その移動方法で位
置を記録する事ができます。
|milfeulle#overlay()| には以下の関数参照を保持した辞書を渡す事が出来ます。


- "is_active()"
	移動先が無効であれば 0 を返す。

- "equal()"
	他の jumper と比較して等価であれば 1 を返す。

- "jump()"
	カーソルの移動を行う。

- "to_string"
	自身の情報を文字列にして返す。


Example: >
	" カーソル位置を保持する jumper
	function! MakeJumper()
		let self = {
	\		"type" : "position",
	\		"pos" : getpos(".")
	\	}
	
		function! self.is_active()
			return self.pos[1] <= line("w$")
		endfunction
	
		function! self.equal(rhs)
			return self.type ==# get(a:rhs, "type", "")
	\			&& self.pos[1] == a:rhs.pos[1]
	\			&& self.pos[2] == a:rhs.pos[2]
		endfunction
	
		function! self.jump()
			call setpos(".", self.pos)
		endfunction
	
		function! self.to_string()
			return printf("lnum : %d  col : %d", self.pos[1], self.pos[2])
		endfunction
		return self
	endfunction
	
	" 現在の位置を記録する
	call milfeulle#overlay(MakeJumper())
<

------------------------------------------------------------------------------
移動方法一覧					*milfeulle-jumper-list*

移動方法はデフォルトでいくつか用意されています。

"win_tab_bufnr_pos"				*milfeulle-jumper-win_tab_bufnr_pos*
	以下のデータを使用します。
	
	"タブ番号"
	"ウィンドウ番号"
	"バッファ番号"
	"カーソル位置"
	
	保持しているタブ番号、ウィンドウ番号、バッファ番号の組み合わせが無効で
	あれば移動に失敗します。

					*milfeulle-jumper-win_tab_bufnr_pos_line*
"win_tab_bufnr_pos_line"
	|milfeulle-jumper-win_tab_bufnr_pos| に加えて行の情報を保持します。
	行が変更されていれば移動に失敗します。

"position"					*milfeulle-jumper-position*
	|getpos()| で取得した情報を使用します
	タブ番号やウィンドウ番号は考慮しません。

"bufnr_pos"					*milfeulle-jumper-bufnr_pos*
	バッファ番号と |getpos()| で取得した情報を使用します
	移動する場合は保存したバッファ番号のバッファをそのウィンドウで開きま
	す。
	|milfeulle-kind-window| を使用する場合に有効です。


==============================================================================
jumper の保持方法				*milfeulle-kind*

|milfeulle-jumper| の保持を行なっている分類です。
いずれかの単位で |milfeulle-jumper| の保持を行うことができます。


"global"					*milfeulle-kind-global*
	Vim 全体で１つのオブジェクトです。

"buffer"					*milfeulle-kind-buffer*
	バッファ単位で保持を行います。

"window"					*milfeulle-kind-window*
	ウィンドウ単位で保持を行います。

"tabpage"					*milfeulle-kind-tabpage*
	タブページ単位で保持を行います。


==============================================================================
設定例						*milfeulle-example*

ウィンドウ単位で保存を行う

Example: >
	" 保持単位をウィンドウに
	let g:milfeulle_default_kind = "window"

	" バッファ番号と位置を保存する
	let g:milfeulle_default_jumper_name = "bufnr_pos"
<


==============================================================================
vim:tw=78:fo=tcq2mM:ts=8:ft=help:norl
